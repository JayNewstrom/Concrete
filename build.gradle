buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply plugin: 'checkstyle'

    checkstyle {
        configFile rootProject.file('checkstyle.xml')
        showViolations true
        configProperties = ['checkstyle.cache.file': rootProject.file('build/checkstyle.cache')]
    }

    task checkstyle(type: Checkstyle) {
        source 'src'
        include '**/*.java'

        // empty classpath
        classpath = files()
    }
}

ext {
    minSdkVersion = 15
    targetSdkVersion = 27
    compileSdkVersion = 27
    buildToolsVersion = '27.0.3'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

ext.deps = [
        supportAnnotations: 'com.android.support:support-annotations:27.1.0',
        appCompatv7: 'com.android.support:appcompat-v7:27.1.0',
        dagger: 'com.google.dagger:dagger:2.15',
        daggerCompiler: 'com.google.dagger:dagger-compiler:2.15',
        jsr305: 'com.google.code.findbugs:jsr305:3.0.2',

        junit: 'junit:junit:4.12',
        fest: 'org.easytesting:fest-assert-core:2.0M10',
        mockitoCore: 'org.mockito:mockito-core:2.8.9',
        androidTestRunner: 'com.android.support.test:runner:1.0.1',
        androidTestRules: 'com.android.support.test:rules:1.0.1',
        espressoCore: 'com.android.support.test.espresso:espresso-core:3.0.1',
]
