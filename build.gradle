ext {
    minSdkVersion = 15
    targetSdkVersion = 28
    compileSdkVersion = 28
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7
    kotlinVersion = '1.2.70'
}

ext.deps = [
        appCompatv7: 'com.android.support:appcompat-v7:27.1.0',
        dagger: 'com.google.dagger:dagger:2.17',
        daggerCompiler: 'com.google.dagger:dagger-compiler:2.17',
        kotlinRuntime: "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",

        junit: 'junit:junit:4.12',
        fest: 'org.easytesting:fest-assert-core:2.0M10',
        mockitoCore: 'org.mockito:mockito-core:2.22.0',
        androidTestRunner: 'com.android.support.test:runner:1.0.1',
        androidTestRules: 'com.android.support.test:rules:1.0.1',
        espressoCore: 'com.android.support.test.espresso:espresso-core:3.0.1',
]

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-rc03'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    buildscript {
        repositories {
            jcenter()
        }
        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
            classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        }
    }

    configureKotlinLint(it)
    configureAndroidPlugin(it)
}

static def configureKotlinLint(Project project) {
    def closure = {
        project.configurations {
            ktlint
        }

        project.dependencies {
            ktlint 'com.github.shyiko:ktlint:0.28.0'
        }

        def ktlintCheck = project.tasks.create('ktlintCheck', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args 'src/**/*.kt'
        }

        project.check.dependsOn ktlintCheck

        project.tasks.create('ktlintFormat', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args '-r', 'src/**/*.kt'
        }
    }
    project.plugins.withId('kotlin-android', closure)
    project.plugins.withId('kotlin', closure)
}

static def configureAndroidPlugin(Project project) {
    def closure = {
        project.android {
            packagingOptions {
                exclude 'META-INF/*.kotlin_module'
                exclude 'kotlin/**'
                exclude 'META-INF/*.version'
            }
        }
    }

    project.plugins.withId('com.android.application', closure)
    project.plugins.withId('com.android.library', closure)
}
